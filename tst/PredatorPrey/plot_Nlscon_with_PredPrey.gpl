#! /usr/bin/gnuplot -p

p0 = system('cat rb_Nlscon_with_PredPrey_parameter.dat | head -2 | tail -1')
p1 = system('cat rb_Nlscon_with_PredPrey_parameter.dat | tail -1')
kbegin = sprintf("with  a = %.3E, b = %.3E, c = %.3E, d = %.3E", word(p0,2)+0, word(p0,3)+0, word(p0,4)+0, word(p0,5)+0)
kend = sprintf("with  a = %.3E, b = %.3E, c = %.3E, d = %.3E", word(p1,2)+0, word(p1,3)+0, word(p1,4)+0, word(p1,5)+0)
headline = "Predator-Prey Model"

set term wxt 0 enhanced
set grid
set xlabel "Time [year AD]"
set ylabel "Population [in thousands]"
set title "Measurement Data"
set key right top

plot for [a=2:3] 'hare_lynx_data.txt' u 1:a t sprintf('data %d',a-1) w lp lc a-1


set term wxt 1 enhanced
set grid
set xlabel "Time [year AD]"
set ylabel "Population [in thousands]"
set title sprintf("%s ;  %s", headline, kbegin)
set key right top

plot for [a=2:3] 'rb_Nlscon_with_PredPrey_solution.dat' i sprintf('== Iter %4d', 0) u 1:a t sprintf("N%d", a-1) w l, for [a=2:3] 'hare_lynx_data.txt' u 1:a t sprintf('data %d',a-1) w p lc a-1


set term wxt 2 enhanced
set grid
set xlabel "Time [year AD]"
set ylabel "Population [in thousands]"
set title sprintf("%s ;  %s", headline, kend)
set key right top

plot for [a=2:3] 'rb_Nlscon_with_PredPrey_solution.dat' i sprintf('== Iter %4d', -1) u 1:a t sprintf("N%d", a-1) w l, for [a=2:3] 'hare_lynx_data.txt' u 1:a t sprintf('data %d',a-1) w p lc a-1


